100 REM *** LIFE from Dec 78 Byte page 76 ***
110 DIM A(22,22),B(22,22),C(4),D(4)
114 REM INITIALIZE A AND B ARRAYS TO ZERO
115 A=0
116 B=0
120 DATA 0,3,0,-3,3,0,-3,0  
130 READ C,D
240 PRINT "L_I_Game of LIFE - Dec 78 Byte Magazine__"
242 PRINT "RULES:"  
243 PRINT "1. Any live cell with fewer than two live neighbors dies."
244 PRINT "2. Any live cell with two or three neighbors lives on."
246 PRINT "3. Any live cell with three or more neighbors dies."
248 PRINT "4. Any dead cell with exactly three live neighbors lives."
255 PRINT "__Enter initial even number of ITERATIONS: ";
256 INPUT L
260 PRINT "__Enter initial Live Cell Coordinates__(0,0 to run pgm):"
270 INPUT X,Y
280 IF X+Y=0 THEN 470
290 IF X<1 OR X>20 OR Y<1 OR Y>20 THEN 310
300 GO TO 330
310 PRINT "__Coordinate ERROR, X and Y must range from 1 to 20"
320 GO TO 260
330 X=X+1
335 Y=Y+1
340 A(X,Y)=A(X,Y)+10
350 FOR X1=X-1 TO X+1
360 FOR Y1=Y-1 TO Y+1
370 A(X1,Y1)=A(X1,Y1)+1
380 NEXT Y1
390 NEXT X1
400 GO TO 270
470 REM RUN THE PROGRAM
475 L1=1
480 FOR I=L1 TO L STEP 2
490 PRINT "L_LIFE Program Iteration Number ";I,"______"
495 REM  DRAW THE OUTLINE AS 91X91
500 MOVE 4,95
510 DRAW 95,95
520 DRAW 95,4
530 DRAW 4,4
540 DRAW 4,95
550 REM  NOW DRAW THE LIVE CELLS AS 3X3 SQUARES
560 FOR Y=1 TO 22
570 FOR X=1 TO 22
580 IF A(X,Y)<10 THEN 584
582 MOVE (X-1)*4.5+0.75,(Y-1)*4.5+0.75
583 RDRAW C,D
584 B(X,Y)=0
590 NEXT X
600 NEXT Y
610 FOR X=2 TO 21
620 FOR Y=2 TO 21
630 A1=A(X,Y)
640 IF (A1<>3) AND (A1<>13) AND (A1<>14) THEN 690
650 B(X,Y)=B(X,Y)+10
660 FOR X1=X-1 TO X+1
665 FOR Y1=Y-1 TO Y+1
670 B(X1,Y1)=B(X1,Y1)+1
675 NEXT Y1
680 NEXT X1
690 NEXT Y
700 NEXT X
710 PRINT "L_LIFE Program Iteration Number ";I+1,"______"
720 REM  DRAW THE OUTLINE AS 91X91
730 MOVE 4,95
740 DRAW 95,95
744 DRAW 95,4
746 DRAW 4,4
748 DRAW 4,95
756 REM  NOW DRAW THE LIVE CELLS AS 2.5X2.5 SQUARES
760 FOR Y=1 TO 22
770 FOR X=1 TO 22
780 IF B(X,Y)<10 THEN 784
782 MOVE (X-1)*4+0.75,(Y-1)*4+0.75
783 RDRAW C,D
784 A(X,Y)=0
790 NEXT X
800 NEXT Y
810 FOR X=2 TO 21
820 FOR Y=2 TO 21
830 B1=B(X,Y)
840 IF (B1<>3) AND (B1<>13) AND (B1<>14) THEN 900
850 A(X,Y)=A(X,Y)+10
860 FOR X1=X-1 TO X+1
865 FOR Y1=Y-1 TO Y+1
870 A(X1,Y1)=A(X1,Y1)+1
875 NEXT Y1
880 NEXT X1
900 NEXT Y
910 NEXT X
920 NEXT I
960 MOVE 96,7
965 PRINT "# of Iterations:";
970 INPUT L2
980 IF L2=0 THEN 1020
990 L=I+L2-1
1000 L1=I
1010 GO TO 480
1020 END