100 REM *** LIFE from Dec 78 Byte page 76 ***
105 M=42
110 DIM L$(M*M),N$(M*M),C(4),D(4)
115 REM initialize RDRAW variables for live cell
120 DATA 0,1.5,0,-1.5,1.5,0,-1.5,0
122 READ C,D
125 C$="0"
131 FOR I=2 TO M
132 C$=C$&"0"
133 NEXT I
134 REM INITIALIZE rest of L$ to "0" 
136 L$=C$
140 FOR I=2 to M
142 L$=L$&C$
144 NEXT I
150 N$=L$
240 PRINT "L_I_Game of LIFE - Dec 78 Byte Magazine__"
242 PRINT "RULES:"  
243 PRINT "1. Any live cell with fewer than two live neighbors dies."
244 PRINT "2. Any live cell with two or three neighbors lives on."
246 PRINT "3. Any live cell with three or more neighbors dies."
248 PRINT "4. Any dead cell with exactly three live neighbors lives."
255 PRINT "__Enter initial even number of ITERATIONS: ";
256 INPUT T
260 PRINT "__Enter initial Live Cell Coordinates__(0,0 to run pgm):"
270 INPUT X,Y
280 IF X+Y=0 THEN 470
290 IF X<1 OR X>M-2 OR Y<1 OR Y>M-2 THEN 310
300 GO TO 330
310 PRINT "__Coordinate ERROR, X and Y must range from 1 to 40"
320 GO TO 260
325 REM put "1" in A$ for live cell at location x,y
330 X=X+1
334 Y=Y+1
335 L$ = REP("1",(X+1)*M+Y+1,1)
340 GO TO 270
460 REM RUN THE PROGRAM
470 L1=0
480 FOR P=L1 TO T
490 PRINT "L_LIFE Program Iteration Number ";P,"______"
495 REM  DRAW THE OUTLINE AS 91X91
500 MOVE 4,95
510 DRAW 95,95
520 DRAW 95,4
530 DRAW 4,4
540 DRAW 4,95
550 REM  NOW DRAW THE LIVE CELLS AS 1.5 X 1.5 SQUARES
560 FOR I=1 TO M-2
570 FOR J=1 TO M-2
575 C$=SEG(L$,(I+1)*M+J+1,1)
580 IF C$="0" THEN 590
582 MOVE (I-1)*2.25+0.37,(J-1)*2.25+0.37
583 RDRAW C,D
590 NEXT J
600 NEXT I
605 REM FIND NUMBER OF NEIGHBORS
610 FOR I=1 TO M-2
620 FOR J=1 TO M-2
630 C$=SEG(L$,(I+1)*M+J+1,1)
640 W=VAL(C$)
650 C$=SEG(L$,(I*M)+(J),1)
652 N=VAL(C$)
654 C$=SEG(L$,(I*M)*(J+1),1)
656 N=N+VAL(C$)
658 C$=SEG(L$,(I*M)*(J+2),1)
660 N=N+VAL(C$)
662 C$=SEG(L$,(I+1)*M+(J),1)
664 N=N+VAL(C$)
666 C$=SEG(L$,(I+1)*M+(J+2),1)
668 N=N+VAL(C$)
670 C$=SEG(L$,(I+2)*M+(J),1)
672 N=N+VAL(C$)
674 C$=SEG(L$,(I+2)*M+(J+1),1)
676 N=N+VAL(C$)
678 C$=SEG(L$,(I+2)*M+(J+2),1)
680 N=N+VAL(C$)
730 IF W=0 THEN 750
740 GO TO 800
750 IF N=3 THEN 780
760 N=0
770 GO TO 850
780 N=1
790 GO TO 850
800 IF N=3 OR N=2 THEN 830
810 N=0
820 GO TO 850
830 N=1
850 C$=STR(N)
855 N$=REP(C$,(I+1)*M+J+1,1)
860 NEXT J
870 NEXT I
880 L$=N$
920 NEXT P
960 MOVE 96,7
965 PRINT "# of Iterations:";
970 INPUT L2
980 IF L2=0 THEN 1020
990 T=P+L2-1
1000 L1=P
1010 GO TO 480
1020 END